! Author: Daisy Leung
!
! Last Modified: 05 Nov 2016
!
! Purpose:
!     Overplot CO21 spectrum on CO32
!
! History
! -------
! 05 Nov 2016:
!     - update redshift
! 27 May 2016:
!     - remove white space
!     - move yaxis label closer to axis
!     - increase pen weight
!
! 04 May 2016:
!     - added code to fit Gauss with fixed FWHM
!     - copied script from Gaussfit_bin2_noCont.greg
!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

begin procedure plot_legend
  g\pen /weight 3
  g\draw relo -3700 &1 /user        ! start point for line
  g\draw line -3500 &1 /user        ! end point for line
  g\draw text -1500 &1 "&2" /user   ! x, y loc, "string"
end procedure plot_legend

begin procedure plot_model
!
! &1 = continuum level, mfit%par[04]
!
  g\set /def xlandscape white
  g\set plot landscape
  set box 3.6 29 5 20
  g\clear
  ! Plot the spectrum
!  g\limits /var x y
  g\limits -2000 2000 -65.0 135  ! x y range
  g\box
!   @ plot_legend &1 "Spectrum"
end procedure plot_model

begin procedure plot_fit
  ! Plot the fit
  ! first fill the histogram
  g\histo /base &1 /fill 7      ! replace 7 with 5 for yellow
  g\histo /fill &2               ! black line superpose on histogram
end procedure plot_fit
!----------------------------------------------

! CO32
column x 1 y 2 /file co32_bin2_D2D3.spec
let x 208.9401-(x/299792.458*208.9401)
let x x*1.65406
let x (((345.7959899-x)/345.7959899)*299792.458)
g\pen 0 /col 0 /dash 1 /weight 2
@ plot_model 100
@ plot_fit 0 0

! if we fit Gauss with fixed width based on CO21 --> prob. just add the flux within FWHM~690 km/s, as in CO21
! say ""
! message i demo "Single Gaussian Fit:"
! mfit y=&a*Exp(-((x-&b)^2|(2*200^2))) /metho powell /start 60,0
! g\connect x mfit%fit

set expand 1.25   ! font size
draw text -1000 115 "\\2 Solid: CO(\iJ=3-2) in RXJ1131" /user
draw text -1000 100 "\\2 Dashed: CO(\iJ=2-1) in RXJ1131" /user
set expand 1
label "Velocity offset [km/s]" /x
label "Flux density [mJy]" /y 4

! CO21
column x 1 y 2 /file sup127_155_2ndcln_noCont.spec

let x 139.256-(x/299792.458*139.256)
let x x*(1+0.65406)
let x (((230.538-x)/230.538)*299792.458)                  ! vel. in 0.65406 frame
g\pen 0 /col 0 /dash 2 /weight 3
@ plot_fit 0 3

! --- draw dotted baseline ------------------------------------
g\pen 0 /col 0 /dash 3
let y 0
connect x y
! @ plot_legend 80 "Baseline"
g\pen 0 /col 0 /dash 1 /weight 3
box  ! draw box

! --- Save ---------------------------------------------------------
sic delete coOverlay.eps
hard coOverlay.eps /dev eps colo
